import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.minecraftforge.gradleutils' version '[2.2,2.3)'
    id 'maven-publish'
    id 'java-library'
}

group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: ${version}"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

license {
    header = file("LICENSE-header.txt")
    newLine = false
}

changelog {
    fromTag '1.0.0'
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Automatic-Module-Name': 'net.minecraftforge.forgespi')
        attributes([
            'Specification-Title': 'forgespi',
            'Specification-Vendor': 'forge',
            'Specification-Version': gradleutils.gitInfo.tag.split('\\.')[0],
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'forge'
        ] as java.util.LinkedHashMap, "net/minecraftforge/forgespi/")
    }
}


dependencies {
    implementation('org.apache.logging.log4j:log4j-api:2.17.0')
    // Needed for Type
    implementation('org.ow2.asm:asm:9.7')
    // Needed for dependency version comparisons
    implementation('org.apache.maven:maven-artifact:3.8.1')
    // Needed for SecureJar API class
    implementation('net.minecraftforge:securemodules:2.2.2')
    // Needed for IEnvironment and ITransformer API
    implementation('net.minecraftforge:modlauncher:10.1.1')
    // Needed for the sided markers
    api('net.minecraftforge:mergetool-api:1.0')
}

// Done down here so that it gets populdted into the pom/module file.
configurations.all {
    dependencies.all {
        transitive false
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'forgespi'
        pom {
            name = 'Forge Service Provider Interface'
            description = 'API for services which can plug into forge, or which are used by forge in other places'
            url = 'https://github.com/MinecraftForge/ForgeSPI'
            PomUtils.setGitHubDetails(pom, 'ForgeSPI')

            license PomUtils.Licenses.LGPLv2_1
            developers {
                developer PomUtils.Developers.cpw
                developer PomUtils.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}